FILE,LAD 2:
SOR,0 XIC,S:1/15 JSR,U:3 EOR,0
RCM,BOTTLE FILL LINE; EXERCISE 5;    written by Lancie1@netease.net;  INITIALIZE VARIABLES - SUBROUTINE 3.
SOR,1 JSR,U:4 EOR,1
RCM,REDUCE MAIN PROGRAM CLUTTER, EXECUTE LED DISPLAY MULTIPLEXING -  SUBROUTINE 4.
SOR,2 JSR,U:5 EOR,2
RCM,EXECUTE CONVEYOR SPEED LIMIT ALARMS - SUBROUTINE 5
SOR,3 XIC,I:1/6 BST,1 BSL,#B3:2,R6:1,I:1/6,32 NXB,1 BST,2 BSL,#B3:4,R6:2,I:1/7,32 NXB,2 BSL,#B3:6,R6:3,I:1/8,32 BND,2 BND,1 EOR,3
RCM,MAIN PROGRAM,                     B3:2/xx = ENCODER POSITION, B3:4/xx = LARGE BOTTLE POSITION, B3:6/xx = BROKEN BOTTLE POSITION
SOR,4 XIC,O:2/1 BST,1 XIC,B3:6/5 NXB,1 BST,2 XIC,B3:6/6 NXB,2 BST,3 XIC,B3:6/7 NXB,3 XIC,B3:6/8 BND,3 BND,2 BND,1 OTE,B3:0/0 EOR,4
RCM,ANTICIPATE BROKEN BOTTLE  SO THAT GRINDER CAN BE STARTED 4 SPACES BEFORE BROKE BOTTLE ARRIVES.
SOR,5 XIC,O:2/10 XIO,B3:0/0 XIO,T4:1/TT XIO,I:1/9 BST,1 XIO,B3:6/9 NXB,1 XIC,I:1/13 BND,1 BST,1 XIO,B3:3/3 NXB,1 BST,2 XIC,B3:6/15 NXB,2 XIC,B3:0/2 BND,2 BND,1 BST,1 XIC,B3:7/3 NXB,1 BST,2 XIO,B3:5/5 NXB,2 XIC,I:1/14 BND,2 BND,1 OTE,O:2/0 EOR,5
RCM,RUN MAIN CONVEYOR MOTOR ONLY UNTIL UNBROKEN BOTTLE IS AT FILLING STATION.
SOR,6 XIC,O:2/10 BST,1 GEQ,N7:2,145 BST,2 XIC,B3:6/6 XIC,B3:4/8 NXB,2 BST,3 XIC,B3:6/7 XIC,B3:4/9 NXB,3 XIC,B3:6/8 XIC,B3:4/10 BND,3 BND,2 NXB,1 BST,2 GEQ,N7:2,150 NXB,2 BST,3 XIC,B3:0/1 NXB,3 BST,4 XIO,I:1/15 NXB,4 XIC,I:1/3 BND,4 BND,3 BND,2 BND,1 OTE,O:2/1 EOR,6
RCM,IF BOX LEVEL = 150, MOVE IT OUT AND START NEW BOX.  IF LEVEL = 145, AND NEXT BROKE BOTTLE IS LARGE BOTTLE, THEN MOVE IT OUT.  (BROKEN BOTTLE = 15 UNITS VOLUME).
SOR,7 XIC,B3:2/3 OTU,B3:0/1 EOR,7
RCM,TURN ON SCRAP CONVEYOR FOR 4 ENCODER PULSES TO MOVE NEW BOX INTO POSITION AT FIRST-SCAN START-UP.
SOR,8 XIC,O:2/10 XIC,I:1/6 XIC,B3:5/5 XIO,B3:7/3 OTE,O:2/5 EOR,8
RCM,IF LARGE UNBROKEN BOTTLE IS AT DIVERT GATE, THEN ENERGIZE DIVERTER.
SOR,9 BST,1 XIC,O:2/10 XIC,I:1/14 NXB,1 XIC,T4:0/TT BND,1 OTE,O:2/2 EOR,9
RCM,IF DIVERTER GATE IS OPEN, THE RUN DIVERT CONVEYOR.  ALSO RUN DIVERT CONVEYOR 4 SECONDS AFTER STOP PRESSED.
SOR,10 BST,1 XIC,O:2/10 NXB,1 XIC,T4:0/TT BND,1 BST,1 XIC,B3:6/8 NXB,1 BST,2 XIC,B3:6/9 NXB,2 BST,3 XIC,B3:6/10 NXB,3 XIC,B3:6/11 BND,3 BND,2 BND,1 OTE,O:2/3 EOR,10
RCM,RUN BOTTLE GRINDER ONLY 4 SPACES BEFORE && 2 AFTER BROKEN BOTTLE ARRIVES, TO PREVENT JAMS, AND SAVE ENERGY.  ALSO RUN 4 SECONDS AFTER STOP PB PRESSED.
SOR,11 XIC,O:2/10 XIC,I:1/6 XIC,B3:6/9 XIO,O:2/1 OTE,O:2/4 EOR,11
RCM,IF BROKE BOTTLE IS AT SCRAP GATE, THEN OPEN GATE.
SOR,12 XIC,O:2/10 XIC,I:1/6 XIC,B3:6/9 BST,1 XIO,B3:4/11 CTU,C5:0,16 NXB,1 XIC,B3:4/11 CTU,C5:1,11 BND,1 EOR,12
RCM,KEEP A COUNT OF HOW MANY SMALL AND LARGE BOTTLES HAVE GONE INTO SCRAP BOX.
SOR,13 XIO,I:1/15 BST,1 RES,C5:0 NXB,1 BST,2 RES,C5:1 NXB,2 CLR,N7:2 BND,2 BND,1 EOR,13
RCM,RESET BROKEN BOTTLE COUNTERS, WHEN OLD BOX LEAVES SCRAP GRINDER.
SOR,14 BST,1 MUL,C5:1.ACC,15,N7:0 NXB,1 BST,2 MUL,C5:0.ACC,10,N7:1 NXB,2 ADD,N7:0,N7:1,N7:2 BND,2 BND,1 EOR,14
RCM,COMPUTE  "VOLUME OF GLASS" FOR SMALL BROKEN BOTTLES IN N7:0, LARGE BROKEN IN N7:1, AND TOTAL IN N7:2.  BOXES CAN HOLD 150.
SOR,15 XIC,O:2/10 XIC,I:1/6 XIC,B3:2/9 XIO,B3:6/5 BST,1 TON,T4:1,11 NXB,1 XIC,T4:1/TT OTE,O:2/6 BND,1 EOR,15
RCM,FILL EXTEND TIME IS THE AVERAGE TOTAL TIME IT TAKES TO EXTEND, FILL, AND RETRACT THE FILL TUBE.
SOR,16 XIC,T4:1/TT TON,T4:2,4 EOR,16
RCM,CHARGE DELAY IS THE TIME IT TAKES TO EXTEND TUBE, BEFORE FILLING CAN BEGIN.
SOR,17 XIC,T4:2/DN TON,T4:3,4 EOR,17
RCM,CHARGE TIME IS THE TIME IT TAKES TO PUMP LIQUID INTO THE BOTTLE.
SOR,18 XIC,O:2/10 XIC,T4:3/TT BST,1 XIC,B3:4/7 OTE,O:2/7 NXB,1 XIO,B3:4/7 OTE,O:2/8 BND,1 EOR,18
RCM,IF LARGE OR SMALL UNBROKEN BOTTLE IS AT FILL TUBE, THEN ENERGIZE THE CORRECT BOTTLE CHARGE.
SOR,19 XIC,I:1/6 BST,1 XIC,I:1/12 NXB,1 XIC,B3:0/2 BND,1 OTE,B3:0/2 EOR,19
RCM,IF THE CAPPER HAS EXTENDED, THEN SET THE CAPPER DONE BIT.
SOR,20 XIC,O:2/10 XIC,I:1/6 XIC,B3:3/3 XIO,B3:6/15 XIO,B3:0/2 OTE,O:2/9 EOR,20
RCM,IF UNBROKEN BOTTLE EXISTS AT THE CAPPER, THEN ENERGIZE THE CAP RAM, UNTIL CAPPER DONE BIT COMES ON.
SOR,21 XIC,I:1/0 XIO,B3:0/3 BST,1 XIC,I:1/1 NXB,1 XIC,O:2/10 BND,1 BST,1 OTE,O:2/10 NXB,1 TOF,T4:0,40 BND,1 EOR,21
RCM,MAIN SYSTEM RUN RELAY.  T4:0 IS TO KEEP DIVERT CONVEYOR AND SCRAP GRINDER RUNNING 4 SECONDS AFTER STOP IS PRESSED.
SOR,22 END,22
RCM,BOTTLE FILL LINE; programmed by Lancie Melson, (931) 724-9576;  Lancie1@netease.net
FILE,SBR 3:
SOR,0 BST,1 RES,C5:0 NXB,1 BST,2 RES,C5:1 NXB,2 BST,3 CLR,N7:0 NXB,3 BST,4 CLR,N7:1 NXB,4 CLR,N7:2 BND,4 BND,3 BND,2 BND,1 EOR,0
RCM,INITIALIZE VARIABLES
SOR,1 BST,1 CLR,B3:2 NXB,1 BST,2 CLR,B3:3 NXB,2 BST,3 CLR,B3:4 NXB,3 BST,4 CLR,B3:5 NXB,4 BST,5 CLR,B3:6 NXB,5 CLR,B3:7 BND,5 BND,4 BND,3 BND,2 BND,1 EOR,1
RCM,ON FIRST SCAN, CLEAR BIT-SHIFT REGISTERS, SO OLD DATA DOES NOT AFFECT NEW BOTTLES.
SOR,2 XIC,S:1/15 OTL,B3:0/1 EOR,2
RCM,
SOR,3 END,3
RCM,
FILE,SBR 4:
SOR,0 XIC,I:1/12 XIO,B3:5/1 CTU,C5:2,32767 EOR,0
RCM,LED DISPLAY ROUTINE 4  - SMALL BOTTLES PRODUCED
SOR,1 XIC,I:1/12 XIC,B3:5/1 CTU,C5:3,32767 EOR,1
RCM,LARGE BOTTLES PRODUCED
SOR,2 XIC,I:1/13 CTU,C5:4,32767 EOR,2
RCM,BOTTLES  SCRAPPED
SOR,3 XIO,I:1/15 CTU,C5:5,32767 EOR,3
RCM,BOXES FILLED
SOR,4 XIC,I:1/1 BST,1 RES,C5:2 NXB,1 BST,2 RES,C5:3 NXB,2 BST,3 RES,C5:4 NXB,3 RES,C5:5 BND,3 BND,2 BND,1 EOR,4
RCM,CLEAR COUNTERS WHEN START PUSHBUTTON IS PRESSED
SOR,5 XIO,T4:4/DN TON,T4:4,10 EOR,5
RCM,START RUNGS TO MOVE PRODUCTION DATA TO LED DISPLAYS
SOR,6 LIM,1,T4:4.ACC,2 BST,1 OTE,O:2/11 NXB,1 TOD,C5:2.ACC,O:4 BND,1 EOR,6
RCM,START RUNGS TO MOVE PRODUCTION DATA TO LED DISPLAYS
SOR,7 LIM,3,T4:4.ACC,4 BST,1 OTE,O:2/12 NXB,1 TOD,C5:3.ACC,O:4 BND,1 EOR,7
RCM,
SOR,8 LIM,5,T4:4.ACC,6 BST,1 OTE,O:2/13 NXB,1 TOD,C5:4.ACC,O:4 BND,1 EOR,8
RCM,
SOR,9 LIM,7,T4:4.ACC,8 BST,1 OTE,O:2/14 NXB,1 TOD,C5:5.ACC,O:4 BND,1 EOR,9
RCM,
SOR,10 LIM,9,T4:4.ACC,10 ADD,C5:2.ACC,C5:3.ACC,N7:4 EOR,10
RCM,
SOR,11 XIC,I:1/5 FRD,I:3,N7:3 EOR,11
RCM,MOVE PRODUCTION THUMBWHEEL SETTING INTO MEMORY WHEN "ENTER" PUSHBUTTON IS PRESSED.
SOR,12 GEQ,N7:4,N7:3 OTE,B3:0/3 EOR,12
RCM,CHECK TO SEE IF THUMBWHEEL PRODUCTION SETTING HAS BEEN REACHED,  IF SO, STOP SYSTEM RUN.
SOR,13 END,13
RCM,
FILE,SBR 5:
SOR,0 XIO,I:1/6 RTO,T4:5,30 EOR,0
RCM,SUB 5:  CONVEYOR SPEED LIMIT ALARMS
SOR,1 XIC,I:1/6 OSR,B3:0/7 BST,1 SUB,106,T4:5.ACC,N7:5 NXB,1 RES,T4:5 BND,1 EOR,1
RCM,N7:5 CONTAINS CONVEYOR SPEED
SOR,2 LIM,95,N7:5,102 OTE,B3:0/4 EOR,2
RCM,CONVEYOR SPEED IS ABOUT RIGHT.
SOR,3 LES,N7:5,95 OTE,B3:0/5 EOR,3
RCM,CONVEYOR SPEED IS TOO SLOW.
SOR,4 GRT,N7:5,102 OTE,B3:0/6 EOR,4
RCM,CONVEYOR SPEED IS TOO FAST !!
SOR,5 END,5
RCM,
FILE,SBR 6:
SOR,0 END,0
RCM,
FILE,SBR 7:
SOR,0 END,0
RCM,
FILE,SBR 8:
SOR,0 END,0
RCM,
FILE,SBR 9:
SOR,0 END,0
RCM,
BTBL
192
0
65535
65535
12336
36208
32770
8224
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
NTBL
45
30
75
2000
70
105
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
FTBL
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
0
SYMBOLS
U:3 INITIALIZE
U:4 UPDATE MUXED LEDS
O:2/0 MAIN  CONVEYOR  MOTOR
O:2/1 SCRAP  CONVEYOR  MOTOR
O:2/2 DIVERT  CONVEYOR  MOTOR
O:2/3 BROKEN BOTTLE GRINDER
O:2/4 SCRAP  GATE
O:2/5 DIVERT  GATE
O:2/6 FILL TUBE
O:2/7 LARGE  BOTTLE  CHARGE
O:2/8 SMALL  BOTTLE  CHARGE
O:2/9 CAP  RAM
O:2/10 SYSTEM  RUN  INDICATOR
I:1/0 STOP  PUSHBUTTON
I:1/1 START PUSHBUTTON
I:1/6 LS1 - ENCODER
B3:5/5 LARGE  AT DIVERT
B3:5/4 LARGE  BOTTLE  1 B4
B3:6/8 BROKE -1 B4 SCRAP GATE
B3:4/10 L/S 1 B4 GATE
C5:0 SMALL  BROKE
C5:1 LARGE  BROKE
I:1/15 LS10 BOX POSITION
T4:0/TT DIVERT, GRINDER POST STOP
T4:0/DN BOX FULL, RESET
B3:0/0 BROKE COMING UP
I:1/3 SEL SW "B"- JOG
B3:6/7 BROKE -2 B4 SCRAP GATE
B3:4/9 LARGE BOTTLE 2 B4
T4:0 RUN  4 SECS. AFTER STOP
B3:6/6 BROKE -3 B4 SCRAP GATE
B3:6/9 BROKE -AT SCRAP GATE
B3:6/11 BROKE -2 AFTER SCRAP GATE
B3:6/12 BROKE -3 AFTER SCRAP GATE
B3:6/10 BROKE -1 AFTER SCRAP GATE
I:1/13 LS8 - SCRAP CLOSED
B3:0/1 NEW SCRAP BOX
S:1/15 FIRST SCAN
B3:6/5 BROKE -4 B4 SCRAP GATE
B3:4/8 LARGE BOTTLE 3 B4
B3:4/11 LARGE/SMALL AT GATE
B3:7/4 BROKE PAST DIVERT
B3:7/2 NOT BROKEN 1B4
B3:2/9 FILL POSITION
S:4/4 FLASH
T4:1 FILL EXTEND TIME
T4:2 CHARGE DELAY
U:5 SPEED LIMIT
T4:3 CHARGE TIMER
T4:1/DN FILL EXTEND TIME
T4:3/TT CHARGE TIME
T4:1/TT FILL EXTEND TIME
T4:2/TT FILL RETRACT TIME
I:1/9 FILL RETRACTED
T4:2/DN CHARGE DELAY
B3:4/7 L/S AT FILL
B3:5/6 LARGE
I:1/14 LS9 -DIVERT OPEN
B3:7/1 NOT BROKEN
T4:4/TT SCRAP GATE TIMER
T4:5 SPEED LIMIT TIMER
T4:5/TT DIVERT GATE TIMER
B3:7/3 NOT BROKEN
B3:2/3 UNLATCH ON 4TH BOTTLE
B3:6/15 BROKEN AT CAPPER
B3:3/3 EXISTS AT CAPPER
I:1/12 CAPPER EXTENDED
B3:0/2 CAPPER DONE
C5:3 LARGE PRODUCED
C5:4 BOTTLES SCRAPPED
C5:5 BOXES FILLED
C5:6 BOXES FILLED
C5:2 SMALL PRODUCED
O:2/12 LARGE PRODUCED MUX
O:2/11 SMALL PRODUCED MUX
O:2/13 BOTTLES SCRAPPED MUX
O:2/14 BOXES FILLED MUX
B3:5/1 L/S AT CAPPER
B3:0/3 STOP IF PRODUCTION REACHED
I:1/5 PRODUCTION ENTER PUSHBUTTON
B3:0/4 SPEED OKAY
B3:0/5 SPEED TOO SLOW
B3:0/6 SPEED TOO FAST
B3:0/7 SPEED ONE-SHOT
